// Top-level build file where you can add configuration options common to all sub-projects/modules.
import org.gradle.api.artifacts.repositories.PasswordCredentials

buildscript {
    ext.kotlin_version = '1.3.70'
    repositories {
      google()
      jcenter()
      mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath 'com.kezong:fat-aar:1.2.9'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

ext {
  internalBuild       = project.hasProperty("internalBuild") && project.internalBuild
  backpackVersion     = "28.0.0${ext.internalBuild || isUseRelative() ? '-internal' : ''}"
  compileSdkVersion   = 29
  targetSdkVersion    = 29
  minSdkVersion       = 21
  buildToolsVersion   = "29.0.2"
  androidx            = '1.0.2'
  appcompat           = '1.0.2'
  supportLibVersion = "27.1.1"
  playServicesVersion = "17.0.0"
  androidMapsUtilsVersion = "xxx"
  threetenabpVersion = "1.2.1"
}

allprojects {
    repositories {
        mavenLocal()
        google()
        jcenter()

        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url "$rootDir/../node_modules/react-native/android"
        }
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../node_modules/jsc-android/dist")
        }

        if (rootProject.ext.internalBuild || isUseRelative()) {
          maven {
            url project.properties["SKYSCANNER_ARTIFACTORY_MAVEN_URL"]?.toString()
            credentials(PasswordCredentials.class) {
              username = project.properties["SKYSCANNER_ARTIFACTORY_MAVEN_USER"]?.toString()
              password = project.properties["SKYSCANNER_ARTIFACTORY_MAVEN_PASSWORD"]?.toString()
            }

            content {
              includeModule("com.github.skyscanner", "backpack-android")
            }
          }
        }

        maven { url 'https://jitpack.io' }

        buildscript {
            repositories {
                google()
                mavenCentral()
            }
        }
    }

    // Force dependency resolution of backpack-android to a specific version
      if (System.getenv("OVERRIDE_BACKPACK_VERSION")) {
        afterEvaluate { project ->
            project.configurations.all {
                resolutionStrategy.eachDependency { DependencyResolveDetails details ->
                    if (details.requested.name == "backpack-android") {
                        details.useVersion System.getenv("OVERRIDE_BACKPACK_VERSION")
                    }
                }
            }
        }
    }
}

allprojects { project ->
  if (project.name == "app" || project.name == "backpack-react-native") {
    configurations {
      ktlint
    }

    dependencies {
      ktlint "com.pinterest:ktlint:0.36.0"
    }

    afterEvaluate {

      def files = project.hasProperty("ktlintFiles") ? project.ktlintFiles : "src/**/*.kt"

      task ktlint(type: JavaExec, group: "verification") {
        description = "Check Kotlin code style."
        classpath = configurations.ktlint
        main = "com.pinterest.ktlint.Main"
        args files, "--editorconfig=${rootProject.projectDir}/.editorconfig"
        // to generate report in checkstyle format prepend following args:
        // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
        // see https://github.com/pinterest/ktlint#usage for more
      }

      check.dependsOn ktlint

      task ktlintFormat(type: JavaExec, group: "formatting") {
        description = "Fix Kotlin code style deviations."
        classpath = configurations.ktlint
        main = "com.pinterest.ktlint.Main"
        args "-F", files, "--editorconfig=${rootProject.projectDir}/.editorconfig"
      }
    }
  }
}

def isUseRelative() {
  Properties localProps = new Properties()
  def localPropsFile = rootProject.file('../.env')
  if (localPropsFile.exists()) {
    localProps.load(localPropsFile.newDataInputStream())
  }

  return localProps.getOrDefault("USE_RELATIVE", "false") == "true"
}

