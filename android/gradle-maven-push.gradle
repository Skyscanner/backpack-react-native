apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'maven-publish'

Properties localProps = new Properties()
def localPropsFile = rootProject.file('local.properties')
if (localPropsFile.exists()) {
    localProps.load(localPropsFile.newDataInputStream())
}

def artifactoryURL = "https://artifactory.skyscannertools.net/artifactory/infrastructure-maven"

project.ext.embedDeps = project.hasProperty("embedDeps") && project.embedDeps == 'true'

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

afterEvaluate { project ->
    project.tasks.bundleReleaseAar.doFirst {
        if (!project.ext.embedDeps) {
            throw new IllegalStateException("To build bridges set the `embedDeps` prop to true. e.g. -PembedDeps=true")
        }
    }

    project.ext.getJFrogPassword = {
        return System.getenv('JFROG_PASSWORD') ?: localProps.getProperty("jfrog_password")
    }

    project.ext.getJFrogUsername = {
        return System.getenv('JFROG_USERNAME') ?: localProps.getProperty("jfrog_username")
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId group
                artifactId project.name
                version version
                description description

                artifact bundleReleaseAar
                artifact sourceJar

                pom.withXml {

                    def dependenciesNode = asNode().appendNode('dependencies')
                    //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                    project.configurations.implementation.allDependencies.each {
                        if (it.group != null && (it.name != null || "unspecified" == it.name) && it.version != null) {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                url "$artifactoryURL"
                credentials {
                    username "${project.getJFrogUsername()}"
                    password "${project.getJFrogPassword()}"
                }
            }
        }
    }

    task checkMavenCredentials(type: Exec) {
        outputs.upToDateWhen { false }
        workingDir './'
        commandLine(['curl',
                     '--silent',
                     '--fail',
                     '-I',
                     '-u',
                     "${project.getJFrogUsername()}:${project.getJFrogPassword()}",
                     "$artifactoryURL"])
    }
}